plugins {
    id "java"
    id "io.qameta.allure" version "2.11.2"
    id "org.springframework.boot" version "3.2.3"
}

jar {
    archiveBaseName = "selenium-webdriver-testng"
    archiveVersion = "1.2.0"
}

compileTestJava {
    sourceCompatibility = 17
    targetCompatibility = 17
    options.compilerArgs += "-parameters"
}

test {
    useTestNG() {
        if (project.hasProperty("groups")) {
            includeGroups "$groups"
        }
        if (project.hasProperty("excludedGroups")) {
            excludeGroups "$excludedGroups"
        }
    }

    scanForTestClasses = false

    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }

    systemProperties System.properties

    if (project.hasProperty("parallel")) {
        maxParallelForks = Runtime.runtime.availableProcessors()
    }

    if (project.hasProperty("suite")) {
        suites "$suite"
    }

    if (project.hasProperty("excludeTests")) {
        "$excludeTests".split(",").each { excludeTests ->
            exclude excludeTests
        }
    }

    ext.failedTests = []

    tasks.withType(Test) {
        afterTest { TestDescriptor descriptor, TestResult result ->
            if(result.resultType == org.gradle.api.tasks.testing.TestResult.ResultType.FAILURE) {
                failedTests << ["${descriptor.className}::${descriptor.name}"]
            }
        }
    }

    gradle.buildFinished {
        if(!failedTests.empty){
            println "Failed test(s) for ${project.name}:"
            failedTests.each { failedTest ->
                println failedTest
            }
        }
    }
}

allure {
    version = "2.16.1"
}

repositories {
    mavenCentral()
    maven {
       url "https://plugins.gradle.org/m2/"
    }
}

ext {
    slf4jVersion  = "2.0.12"
    logbackVersion  = "1.5.3"

    seleniumVersion  = "4.18.1"
    testNgVersion = "7.9.0"
    assertjVersion  = "3.25.3"
    wdmVersion = "5.7.0"

    htmlunitVersion = "4.13.0"
    awaitilityVersion = "4.2.1"
    browsermobVersion = "2.1.5"
    zapclientVersion = "1.13.0"
    axelVersion = "4.8.2"
    selenideVersion = "7.2.2"
    datafakerVersion = "2.1.0"
    extentreportsVersion = "5.1.1"
    cucumberVersion = "7.15.0"
    springBootVersion = "3.2.3"
    appiumVersion = "9.2.0"
    restAssuredVersion = "5.4.0"
}

dependencies {
    implementation("org.slf4j:slf4j-api:${slf4jVersion}")
    implementation("ch.qos.logback:logback-classic:${logbackVersion}")

    testImplementation("org.seleniumhq.selenium:selenium-java:${seleniumVersion}")
    testImplementation("org.testng:testng:${testNgVersion}")
    testImplementation("org.assertj:assertj-core:${assertjVersion}")
    testImplementation("io.github.bonigarcia:webdrivermanager:${wdmVersion}") {
        exclude group: "org.bouncycastle", module: "*"
    }

    testImplementation("org.seleniumhq.selenium:htmlunit-driver:${htmlunitVersion}") {
        exclude group: "org.seleniumhq.selenium", module: "*"
    }
    testImplementation("org.seleniumhq.selenium:selenium-grid:${seleniumVersion}") {
        exclude group: "org.bouncycastle", module: "*"
    }
    testImplementation("org.awaitility:awaitility:${awaitilityVersion}")
    testImplementation("net.lightbody.bmp:browsermob-core:${browsermobVersion}") {
        exclude group: "com.fasterxml.jackson.core", module: "*"
    }
    testImplementation("org.zaproxy:zap-clientapi:${zapclientVersion}")
    testImplementation("com.deque.html.axe-core:selenium:${axelVersion}") {
        exclude group: "org.seleniumhq.selenium", module: "*"
    }
    testImplementation("com.codeborne:selenide:${selenideVersion}") {
        exclude group: "org.seleniumhq.selenium", module: "selenium-java"
        exclude group: "io.github.bonigarcia", module: "webdrivermanager"
        exclude group: "org.apache.httpcomponents.client5", module: "httpclient5"
    }
    testImplementation("net.datafaker:datafaker:${datafakerVersion}")
    testImplementation("com.aventstack:extentreports:${extentreportsVersion}") {
        exclude group: "org.junit.jupiter", module: "*"
    }
    testImplementation("io.cucumber:cucumber-java:${cucumberVersion}")
    testImplementation("io.cucumber:cucumber-testng:${cucumberVersion}")
    implementation("org.springframework.boot:spring-boot-starter:${springBootVersion}")
    implementation("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
    testImplementation("org.springframework.boot:spring-boot-starter-test:${springBootVersion}") {
        exclude group: "org.projectlombok", module: "lombok"
    }
    testImplementation("io.appium:java-client:${appiumVersion}") {
        exclude group: "org.seleniumhq.selenium", module: "*"
    }
    testImplementation("io.rest-assured:rest-assured:${restAssuredVersion}")
}
