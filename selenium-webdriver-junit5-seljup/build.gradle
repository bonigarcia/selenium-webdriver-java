plugins {
    id "java"
    id "io.qameta.allure" version "2.11.2"
    id "org.springframework.boot" version "3.1.4"
}

jar {
    archiveBaseName = "selenium-webdriver-junit5-seljup"
    archiveVersion = "1.2.0"
}

compileTestJava {
    sourceCompatibility = 17
    targetCompatibility = 17
    options.compilerArgs += "-parameters"
}

test {
    useJUnitPlatform() {
        if (project.hasProperty("groups")) {
            includeTags "$groups"
        }
        if (project.hasProperty("excludedGroups")) {
            excludeTags "$excludedGroups"
        }
    }

    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }

    System.setProperty("webdriver.http.factory", "jdk-http-client")
    systemProperties System.properties

    if (project.hasProperty("excludeTests")) {
        "$excludeTests".split(",").each { excludeTests ->
            exclude excludeTests
        }
    }

    if (project.hasProperty("parallel")) {
        maxParallelForks = Runtime.runtime.availableProcessors()
    }

    ext.failedTests = []

    tasks.withType(Test) {
        afterTest { TestDescriptor descriptor, TestResult result ->
            if(result.resultType == org.gradle.api.tasks.testing.TestResult.ResultType.FAILURE) {
                failedTests << ["${descriptor.className}::${descriptor.name}"]
            }
        }
    }

    gradle.buildFinished {
        if(!failedTests.empty){
            println "Failed test(s) for ${project.name}:"
            failedTests.each { failedTest ->
                println failedTest
            }
        }
    }
}

allure {
    version = "2.16.1"
}

repositories {
    mavenCentral()
    maven {
       url "https://plugins.gradle.org/m2/"
    }
}

ext {
    slf4jVersion  = "2.0.9"
    logbackVersion  = "1.4.11"

    //seleniumVersion  = "4.14.1"
    seleniumVersion  = "4.13.0"

    junit5Version = "5.10.0"
    selJupVersion = "4.3.7"
    assertjVersion  = "3.24.2"

    htmlunitVersion = "4.13.0"
    rerunnerVersion = "2.1.6"
    junitPlatformVersion = "1.10.0"
    awaitilityVersion = "4.2.0"
    browsermobVersion = "2.1.5"
    zapclientVersion = "1.12.0"
    axelVersion = "4.8.0"
    selenideVersion = "6.19.1"
    browserupProxyVersion = "2.1.2"

    datafakerVersion = "2.0.2"
    extentreportsVersion = "5.1.1"
    cucumberVersion = "7.14.0"
    springBootVersion = "3.1.4"
    appiumVersion = "9.0.0"
    restAssuredVersion = "5.3.1"
}


dependencies {
    implementation("org.slf4j:slf4j-api:${slf4jVersion}")
    implementation("ch.qos.logback:logback-classic:${logbackVersion}")

    testImplementation("org.seleniumhq.selenium:selenium-java:${seleniumVersion}")
    testImplementation("org.seleniumhq.selenium:selenium-http-jdk-client:${seleniumVersion}")
    testImplementation("org.junit.jupiter:junit-jupiter:${junit5Version}")
    testImplementation("io.github.bonigarcia:selenium-jupiter:${selJupVersion}")
    testImplementation("org.assertj:assertj-core:${assertjVersion}")

    testImplementation("org.seleniumhq.selenium:htmlunit-driver:${htmlunitVersion}") {
        exclude group: "org.seleniumhq.selenium", module: "*"
    }
    testImplementation("org.seleniumhq.selenium:selenium-grid:${seleniumVersion}")
    testImplementation("io.github.artsok:rerunner-jupiter:${rerunnerVersion}")
    testImplementation("org.awaitility:awaitility:${awaitilityVersion}")
    testImplementation("org.junit.platform:junit-platform-launcher:${junitPlatformVersion}")
    testImplementation("net.lightbody.bmp:browsermob-core:${browsermobVersion}") {
        exclude group: "com.fasterxml.jackson.core", module: "*"
        exclude group: "io.netty", module: "*"
    }
    testImplementation("org.zaproxy:zap-clientapi:${zapclientVersion}")
    testImplementation("com.deque.html.axe-core:selenium:${axelVersion}") {
        exclude group: "org.seleniumhq.selenium", module: "*"
        exclude group: "com.fasterxml.jackson.core", module: "*"
    }
    testImplementation("com.codeborne:selenide:${selenideVersion}") {
        exclude group: "org.seleniumhq.selenium", module: "selenium-java"
        exclude group: "io.github.bonigarcia", module: "webdrivermanager"
        exclude group: "org.apache.httpcomponents.client5", module: "httpclient5"
    }
    testImplementation("com.browserup:browserup-proxy-core:${browserupProxyVersion}") {
        exclude group: "com.fasterxml.jackson.core", module: "*"
        exclude group: "io.netty", module: "*"
    }
    testImplementation("net.datafaker:datafaker:${datafakerVersion}")
    testImplementation("com.aventstack:extentreports:${extentreportsVersion}") {
        exclude group: "org.projectlombok", module: "lombok"
    }
    testImplementation("org.junit.platform:junit-platform-suite:${junitPlatformVersion}")
    testImplementation("io.cucumber:cucumber-java:${cucumberVersion}")
    testImplementation("io.cucumber:cucumber-junit-platform-engine:${cucumberVersion}")
    implementation("org.springframework.boot:spring-boot-starter:${springBootVersion}")
    implementation("org.springframework.boot:spring-boot-starter-web:${springBootVersion}") {
        exclude group: "com.fasterxml.jackson.core", module: "*"
    }
    testImplementation("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
    testImplementation("io.appium:java-client:${appiumVersion}") {
        exclude group: "org.seleniumhq.selenium", module: "*"
    }
    testImplementation("io.rest-assured:rest-assured:${restAssuredVersion}")

    // https://mvnrepository.com/artifact/org.seleniumhq.selenium/selenium-devtools-v114
    testImplementation 'org.seleniumhq.selenium:selenium-devtools-v114:4.12.1'
}
