plugins {
    id "java"
    id "io.qameta.allure" version "2.12.0"
    id "org.springframework.boot" version "3.4.3"
}

jar {
    archiveBaseName = "selenium-webdriver-junit5-seljup"
    archiveVersion = "1.2.0"
}

compileTestJava {
    sourceCompatibility = 17
    targetCompatibility = 17
    options.compilerArgs += "-parameters"
}

test {
    useJUnitPlatform() {
        if (project.hasProperty("groups")) {
            includeTags "$groups"
        }
        if (project.hasProperty("excludedGroups")) {
            excludeTags "$excludedGroups"
        }
    }

    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }

    systemProperties System.properties

    if (project.hasProperty("excludeTests")) {
        "$excludeTests".split(",").each { excludeTests ->
            exclude excludeTests
        }
    }

    if (project.hasProperty("parallel")) {
        maxParallelForks = Runtime.runtime.availableProcessors()
    }

    ext.failedTests = []

    tasks.withType(Test) {
        afterTest { TestDescriptor descriptor, TestResult result ->
            if(result.resultType == org.gradle.api.tasks.testing.TestResult.ResultType.FAILURE) {
                failedTests << ["${descriptor.className}::${descriptor.name}"]
            }
        }
    }

    gradle.buildFinished {
        if(!failedTests.empty){
            println "Failed test(s) for ${project.name}:"
            failedTests.each { failedTest ->
                println failedTest
            }
        }
    }
}

allure {
    version = "2.16.1"
}

repositories {
    mavenCentral()
    maven {
       url = "https://plugins.gradle.org/m2/"
    }
    maven {
       url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

ext {
    slf4jVersion  = "2.0.17"
    logbackVersion  = "1.5.17"

    seleniumVersion  = "4.29.0"
    junit5Version = "5.12.0"
    selJupVersion = "6.0.0-SNAPSHOT"
    assertjVersion  = "3.27.3"

    htmlunitVersion = "4.13.0"
    rerunnerVersion = "2.1.6"
    junitPlatformVersion = "1.12.0"
    awaitilityVersion = "4.3.0"
    browsermobVersion = "2.1.5"
    zapclientVersion = "1.16.0"
    axelVersion = "4.10.1"
    selenideVersion = "7.7.3"
    browserupProxyVersion = "2.1.2"

    datafakerVersion = "2.4.2"
    extentreportsVersion = "5.1.2"
    cucumberVersion = "7.21.1"
    springBootVersion = "3.4.3"
    appiumVersion = "9.4.0"
    restAssuredVersion = "5.5.1"
}


dependencies {
    implementation("org.slf4j:slf4j-api:${slf4jVersion}")
    implementation("ch.qos.logback:logback-classic:${logbackVersion}")

    testImplementation("org.seleniumhq.selenium:selenium-java:${seleniumVersion}")
    testImplementation("org.junit.jupiter:junit-jupiter:${junit5Version}")
    testImplementation("io.github.bonigarcia:selenium-jupiter:${selJupVersion}")
    testImplementation("org.assertj:assertj-core:${assertjVersion}")

    testImplementation("org.seleniumhq.selenium:htmlunit-driver:${htmlunitVersion}") {
        exclude group: "org.seleniumhq.selenium", module: "*"
    }
    testImplementation("org.seleniumhq.selenium:selenium-grid:${seleniumVersion}") {
        exclude group: "org.bouncycastle", module: "*"
    }
    testImplementation("io.github.artsok:rerunner-jupiter:${rerunnerVersion}")
    testImplementation("org.awaitility:awaitility:${awaitilityVersion}")
    testImplementation("org.junit.platform:junit-platform-launcher:${junitPlatformVersion}")
    testImplementation("net.lightbody.bmp:browsermob-core:${browsermobVersion}") {
        exclude group: "com.fasterxml.jackson.core", module: "*"
        exclude group: "io.netty", module: "*"
    }
    testImplementation("org.zaproxy:zap-clientapi:${zapclientVersion}")
    testImplementation("com.deque.html.axe-core:selenium:${axelVersion}") {
        exclude group: "org.seleniumhq.selenium", module: "*"
        exclude group: "com.fasterxml.jackson.core", module: "*"
    }
    testImplementation("com.codeborne:selenide:${selenideVersion}") {
        exclude group: "org.seleniumhq.selenium", module: "selenium-java"
        exclude group: "io.github.bonigarcia", module: "webdrivermanager"
        exclude group: "org.apache.httpcomponents.client5", module: "httpclient5"
    }
    testImplementation("com.browserup:browserup-proxy-core:${browserupProxyVersion}") {
        exclude group: "com.fasterxml.jackson.core", module: "*"
        exclude group: "io.netty", module: "*"
    }
    testImplementation("net.datafaker:datafaker:${datafakerVersion}")
    testImplementation("com.aventstack:extentreports:${extentreportsVersion}") {
        exclude group: "org.projectlombok", module: "lombok"
    }
    testImplementation("org.junit.platform:junit-platform-suite:${junitPlatformVersion}")
    testImplementation("io.cucumber:cucumber-java:${cucumberVersion}")
    testImplementation("io.cucumber:cucumber-junit-platform-engine:${cucumberVersion}")
    implementation("org.springframework.boot:spring-boot-starter:${springBootVersion}")
    implementation("org.springframework.boot:spring-boot-starter-web:${springBootVersion}") {
        exclude group: "com.fasterxml.jackson.*", module: "*"
    }
    testImplementation("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
    testImplementation("io.appium:java-client:${appiumVersion}") {
        exclude group: "org.seleniumhq.selenium", module: "*"
    }
    testImplementation("io.rest-assured:rest-assured:${restAssuredVersion}")
}
