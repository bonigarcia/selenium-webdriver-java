plugins {
    id "java"
    id "io.qameta.allure" version "2.9.6"
    id "org.springframework.boot" version "2.5.6"
}

jar {
    archiveBaseName = "selenium-webdriver-junit5-seljup"
    archiveVersion = "1.0.0-SNAPSHOT"
}

compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.compilerArgs += "-parameters"
}

test {
    useJUnitPlatform() {
        if (project.hasProperty("groups")) {
            includeTags "$groups"
        }
        if (project.hasProperty("excludedGroups")) {
            excludeTags "$excludedGroups"
        }
    }

    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }

    systemProperties System.properties

    if (project.hasProperty("excludeTests")) {
        "$excludeTests".split(",").each { excludeTests ->
            exclude excludeTests
        }
    }

    if (project.hasProperty("parallel")) {
        maxParallelForks = Runtime.runtime.availableProcessors()
    }

    ext.failedTests = []

    tasks.withType(Test) {
        afterTest { TestDescriptor descriptor, TestResult result ->
            if(result.resultType == org.gradle.api.tasks.testing.TestResult.ResultType.FAILURE) {
                failedTests << ["${descriptor.className}::${descriptor.name}"]
            }
        }
    }

    gradle.buildFinished {
        if(!failedTests.empty){
            println "Failed test(s) for ${project.name}:"
            failedTests.each { failedTest ->
                println failedTest
            }
        }
    }
}

allure {
    version = "2.16.1"
}

repositories {
    mavenCentral()
    maven {
       url "https://plugins.gradle.org/m2/"
    }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

ext {
    slf4jVersion  = "1.7.32"
    logbackVersion  = "1.2.7"

    seleniumVersion  = "4.0.0"
    junit5Version = "5.8.1"
    selJupVersion = "4.0.2-SNAPSHOT"
    assertjVersion  = "3.21.0"

    rerunnerVersion = "2.1.6"
    junitPlatformVersion = "1.8.1"
    browsermobVersion = "2.1.5"
    zapclientVersion = "1.10.0"
    axelVersion = "4.3.1"
    javafakerVersion = "1.0.2"
    extentreportsVersion = "5.0.9"
    cucumberVersion = "7.0.0"
    springBootVersion = "2.5.7"
}


dependencies {
    implementation("org.slf4j:slf4j-api:${slf4jVersion}")
    implementation("ch.qos.logback:logback-classic:${logbackVersion}")

    testImplementation("org.seleniumhq.selenium:selenium-java:${seleniumVersion}")
    testImplementation("org.junit.jupiter:junit-jupiter:${junit5Version}")
    testImplementation("io.github.bonigarcia:selenium-jupiter:${selJupVersion}")
    testImplementation("org.assertj:assertj-core:${assertjVersion}")

    testImplementation("org.seleniumhq.selenium:selenium-grid:${seleniumVersion}")
    testImplementation("io.github.artsok:rerunner-jupiter:${rerunnerVersion}")
    testImplementation("org.junit.platform:junit-platform-launcher:${junitPlatformVersion}")
    testImplementation("net.lightbody.bmp:browsermob-core:${browsermobVersion}") {
        exclude group: "com.fasterxml.jackson.core", module: "*"
    }
    testImplementation("org.zaproxy:zap-clientapi:${zapclientVersion}")
    testImplementation("com.deque.html.axe-core:selenium:${axelVersion}")
    testImplementation("com.github.javafaker:javafaker:${javafakerVersion}")
    testImplementation("com.aventstack:extentreports:${extentreportsVersion}")
    testImplementation("org.junit.platform:junit-platform-suite:${junitPlatformVersion}")
    testImplementation("io.cucumber:cucumber-java:${cucumberVersion}")
    testImplementation("io.cucumber:cucumber-junit-platform-engine:${cucumberVersion}")

    implementation("org.springframework.boot:spring-boot-starter:${springBootVersion}")
    implementation("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
    testImplementation("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
}
