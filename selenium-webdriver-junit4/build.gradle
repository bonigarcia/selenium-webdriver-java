plugins {
    id "java"
    id "io.qameta.allure" version "3.0.1"
    id "org.springframework.boot" version "4.0.0"
}

jar {
    archiveBaseName = "selenium-webdriver-junit4"
    archiveVersion = "1.2.0"
}

compileTestJava {
    sourceCompatibility = 17
    targetCompatibility = 17
    options.compilerArgs += "-parameters"
}

test {
    useJUnit() {
        if (project.hasProperty("groups")) {
            includeCategories "$groups"
        }
        if (project.hasProperty("excludedGroups")) {
            excludeCategories "$excludedGroups"
        }
    }

    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }

    systemProperties System.properties

    if (project.hasProperty("excludeTests")) {
        "$excludeTests".split(",").each { excludeTests ->
            exclude excludeTests
        }
    }

    if (project.hasProperty("parallel")) {
        maxParallelForks = Runtime.runtime.availableProcessors()
    }

    ext.failedTests = []

    tasks.withType(Test) {
        afterTest { TestDescriptor descriptor, TestResult result ->
            if(result.resultType == org.gradle.api.tasks.testing.TestResult.ResultType.FAILURE) {
                failedTests << ["${descriptor.className}::${descriptor.name}"]
            }
        }
    }

    gradle.buildFinished {
        if(!failedTests.empty){
            println "Failed test(s) for ${project.name}:"
            failedTests.each { failedTest ->
                println failedTest
            }
        }
    }
}

allure {
    version = "2.16.1"
}

repositories {
    mavenCentral()
    maven {
       url = "https://plugins.gradle.org/m2/"
    }
}

ext {
    slf4jVersion  = "2.0.17"
    logbackVersion  = "1.5.21"

    seleniumVersion  = "4.38.0"
    junit4Version = "4.13.2"
    assertjVersion  = "3.27.6"
    wdmVersion = "6.3.3"

    htmlunitVersion = "4.13.0"
    awaitilityVersion = "4.3.0"
    browsermobVersion = "2.1.5"
    zapclientVersion = "1.16.0"
    axelVersion = "4.11.0"
    selenideVersion = "7.12.1"
    datafakerVersion = "2.5.3"
    extentreportsVersion = "5.1.2"
    cucumberVersion = "7.32.0"
    springBootVersion = "4.0.0"
    appiumVersion = "10.0.0"
    restAssuredVersion = "5.5.6"
}

dependencies {
    implementation("org.slf4j:slf4j-api:${slf4jVersion}")
    implementation("ch.qos.logback:logback-classic:${logbackVersion}")

    testImplementation("org.seleniumhq.selenium:selenium-java:${seleniumVersion}")
    testImplementation("junit:junit:${junit4Version}")
    testImplementation("org.assertj:assertj-core:${assertjVersion}")
    testImplementation("io.github.bonigarcia:webdrivermanager:${wdmVersion}")

    testImplementation("org.seleniumhq.selenium:htmlunit-driver:${htmlunitVersion}") {
        exclude group: "org.seleniumhq.selenium", module: "*"
    }
    testImplementation("org.seleniumhq.selenium:selenium-grid:${seleniumVersion}") {
        exclude group: "org.bouncycastle", module: "*"
    }
    testImplementation("org.awaitility:awaitility:${awaitilityVersion}")
    testImplementation("net.lightbody.bmp:browsermob-core:${browsermobVersion}") {
        exclude group: "com.fasterxml.jackson.core", module: "*"
    }
    testImplementation("org.zaproxy:zap-clientapi:${zapclientVersion}")
    testImplementation("com.deque.html.axe-core:selenium:${axelVersion}") {
        exclude group: "org.seleniumhq.selenium", module: "*"
    }
    testImplementation("com.codeborne:selenide:${selenideVersion}") {
        exclude group: "org.seleniumhq.selenium", module: "selenium-java"
        exclude group: "io.github.bonigarcia", module: "webdrivermanager"
        exclude group: "org.apache.httpcomponents.client5", module: "httpclient5"
    }
    testImplementation("net.datafaker:datafaker:${datafakerVersion}")
    testImplementation("com.aventstack:extentreports:${extentreportsVersion}") {
        exclude group: "org.projectlombok", module: "lombok"
    }
    testImplementation("io.cucumber:cucumber-java:${cucumberVersion}")
    testImplementation("io.cucumber:cucumber-junit:${cucumberVersion}")
    implementation("org.springframework.boot:spring-boot-starter:${springBootVersion}")
    implementation("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
    testImplementation("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
    testImplementation("io.appium:java-client:${appiumVersion}") {
        exclude group: "org.seleniumhq.selenium", module: "*"
    }
    testImplementation("io.rest-assured:rest-assured:${restAssuredVersion}")
}
